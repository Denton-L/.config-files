"define $VIMHOME
let $VIMHOME=expand('<sfile>:p:h')

"more history
set history=2000

"line numbering
set number
set relativenumber

"tab defaults
set shiftwidth=0
set autoindent
set smarttab

"set display options
set scrolloff=1
set sidescrolloff=5
set display+=lastline
set cursorlineopt=line

"statusline defaults
set laststatus=2
set ruler

"enable completion for command-line
set wildmenu
set wildmode=longest:full
set wildcharm=<Tab>
cnoremap <Tab> <Space><BS><Tab>

"make <C-p> and <C-n> perform searching
cnoremap <expr> <C-p> wildmenumode() ? "\<C-p>" : "\<Up>"
cnoremap <expr> <C-n> wildmenumode() ? "\<C-n>" : "\<Down>"

"change spellcheck to english
set spelllang=en

"use only one space between sentences
set nojoinspaces

"enable sensible splits
set splitbelow
set splitright

"indent by filetype
filetype plugin indent on

"enable syntax highlighting
syntax on

"set dark background with hack to fix xterm support
set t_RS=
set background=dark

"search defaults
set incsearch

"delete comment character when joining commented lines
set formatoptions+=j

"automatically re-read file if changed
set autoread

"disable the mouse
set mouse=

"set the listchars
set listchars=tab:>-,trail:~,extends:>,precedes:<,nbsp:+

"define thesaurus
set thesaurus+=$VIMHOME/thesauruses/mthesaur.txt

"allow switching from modified buffers
set hidden

"look for tags in parent folders
set tags=tags;/

"search for files recursively
set path+=**

"use vi-like backspace
set backspace=

"ensure visual mode $ doesn't delete newline
set selection=old

"use blowfish2 as default encryption method
try
	set cryptmethod=blowfish2
catch
endtry

"change timeout settings
set notimeout
set ttimeout
set ttimeoutlen=0

"auto-indent code on cu (clean up)
nnoremap cu gg=G``zz

"search selection with Leader-s
nnoremap <Leader>s *N
vnoremap <Leader>s y/<C-r>"<CR>``

"navigate wrapped lines with ease
nnoremap <expr> j v:count ? 'j' : 'gj'
nnoremap <expr> k v:count ? 'k' : 'gk'
nnoremap <expr> <Up> v:count ? 'k' : 'gk'
nnoremap <expr> <Down> v:count ? 'j' : 'gj'

"remove trailing spaces with Leader-t
nnoremap <Leader>t :%s/\s\+$//<CR>:nohlsearch<CR>
vnoremap <Leader>t :s/\s\+$//<CR>:nohlsearch<CR>

"break undoing sequence when doing deletes
inoremap <C-u> <C-g>u<C-u>
inoremap <C-w> <C-g>u<C-w>

"allow <C-z> in insert mode
inoremap <C-z> <C-o><C-z>

"auto-source *vimrc on save
augroup vimrc_source
	autocmd!
	autocmd BufWritePost *vimrc source %
augroup END

"make Y behave like D and C
nnoremap Y y$

"smarter tabline
let g:airline#extensions#tabline#enabled = 1
let g:airline#extensions#tabline#show_close_button = 0

"default to straight separators
let g:airline_symbols_ascii = 1
let g:airline_left_sep = ' '
let g:airline_left_alt_sep = '|'
let g:airline_right_sep = ' '
let g:airline_right_alt_sep = '|'

"allow mixed indent for /* */ comments
let g:airline#extensions#whitespace#mixed_indent_algo = 1

"change vimtex leader key
let g:vimtex_imaps_leader = '~'

"make vim-localvimrc make loaded files persistent
let g:localvimrc_persistent = 1

"make go and gO start at the beginning of the line
nnoremap go o<Esc>0C
nnoremap gO O<Esc>0C

"make S start from the beginning of the line
nnoremap S S<Esc>0C
vnoremap S S<Esc>0C

"make <Leader>o/O open (unjoin) lines
nnoremap <Leader>o i<CR><Esc>
nnoremap <Leader>O a<CR><Esc>

"make <Leader>d clear lines
nnoremap <Leader>d 0D

"make dm and cm (eliMinate) delete into blackhole register
nnoremap dm "_d
nnoremap dmm "_dd
nnoremap dM "_D
nnoremap cm "_c
nnoremap cmm "_cc
nnoremap cM "_C

"make dc and yc go to clipboard
nnoremap dc "+d
nnoremap dcc "+dd
nnoremap dC "+D
nnoremap yc "+y
nnoremap ycc "+yy
nmap yC "+Y

"use tjump by default instead of tag
nnoremap <C-]> g<C-]>
vnoremap <C-]> g<C-]>

"save to temporary file
command! MkTemp execute (bufname('%') == '' ? 'write' : 'saveas') . '! ' . system('mktemp')

"hex-editing tools
command! HexDump %!xxd

function! UnHexDump()
	silent! %substitute/\C^[0-9a-f]*:\(\%( [0-9a-f]\{1,4}\)* *\)  .*$/\1/
	%!xxd -r -p
endfunction
command! UnHexDump call UnHexDump()
